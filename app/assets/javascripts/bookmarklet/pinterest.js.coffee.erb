//= require templates/bookmarklet/pinterest/our_boards
//= require templates/bookmarklet/pinterest/step_1
//= require templates/bookmarklet/pinterest/step_2_load
//= require underscore.min

# Note - maybe if eval in getScript (or just $.get callback, can pass in vars from other script?)

# Config
outputDiv         = $('#ppBookmarkletContent')
outputDivWrapper  = outputDiv.parents('#ppBookmarkletContentWrapper')
progressDiv       = outputDiv.find('#noPotentialImages') # TODO -- pass this in in case there's another with this ID in the original page's HTML
boardSelector     = '.UserBoards .item.gridSortable a.boardLinkWrapper'
pinSelector       = '.pinWrapper'
host              = '//<%= ActionMailer::Base.default_url_options[:host] + (ActionMailer::Base.default_url_options[:port] == 80 ? '' : ":#{ActionMailer::Base.default_url_options[:port]}") %>'
stepOneCSSUrl     = host + "<%= asset_path('import-pinterest.css') %>"
stepOneJSUrl      = host + "<%= asset_path('import-pinterest.js') %>"
boardsUrl         = host + '<%= Rails.application.routes.url_helpers.my_boards_json_path %>?callback=?' # Force jsonp request
pinsUrl           = host + '<%= Rails.application.routes.url_helpers.my_pins_json_path %>?callback=?' # Force jsonp request
stepTwoURL        = host + '<%= Rails.application.routes.url_helpers.pinterest_import_step_2_path %>' # Loaded in iframe directly from PP
debug             = false
useTestData       = true

# When viewing an image directly, the browser creates minimal surrounding HTML that doesn't include a HEAD section
appendTarget = document.getElementsByTagName("head")[0] || document.getElementsByTagName("body")[0]

testData = [{"name":"Default","pinterestURL":"http://pinterest.com/dvntpins/default/","id": 236459021, "pins":[{"description":"Brandy Melville Tank $28","domain":"saltandseaweed.com","price":"$28.00","smallImageURL":"http://media-cache-ak1.pinimg.com/236x/06/49/09/06490959f16ea755c89e3b444a9e5686.jpg","pinterestURL":"http://pinterest.com/pin/442126888388601214/","id":511140869,"link":"http://www.saltandseaweed.com/collections/vendors?q=Brandy+Melville","imageURL":"http://media-cache-ak1.pinimg.com/736x/06/49/09/06490959f16ea755c89e3b444a9e5686.jpg"},{"description":"That's How a Pro Uses a Bunk Bed","domain":"memebase.cheezburger.com","price":"","smallImageURL":"http://media-cache-ak1.pinimg.com/236x/6e/06/34/6e063495136bfc91b8cfac6cd55e8557.jpg","pinterestURL":"http://pinterest.com/pin/442126888388218505/","id":1291275682,"link":"http://memebase.cheezburger.com/senorgif","imageURL":"http://media-cache-ak1.pinimg.com/736x/6e/06/34/6e063495136bfc91b8cfac6cd55e8557.jpg"},{"description":"ireland - Google Search","domain":"travel.nytimes.com","price":"","smallImageURL":"http://media-cache-ak1.pinimg.com/236x/49/4b/af/494bafca94dd0e2168fb4c8c7f914c78.jpg","pinterestURL":"http://pinterest.com/pin/442126888388183279/","id":599475931,"link":"http://travel.nytimes.com/travel/guides/europe/ireland/overview.html","imageURL":"http://media-cache-ak1.pinimg.com/736x/49/4b/af/494bafca94dd0e2168fb4c8c7f914c78.jpg"}]},{"name":"Secondary","pinterestURL":"http://pinterest.com/dvntpins/secondary/","id": 141808096, "pins":[{"description":"Brandy Melville Tank $28","domain":"saltandseaweed.com","price":"$28.00","smallImageURL":"http://media-cache-ak1.pinimg.com/236x/06/49/09/06490959f16ea755c89e3b444a9e5686.jpg","pinterestURL":"http://pinterest.com/pin/442126888388601214/","id":511140869,"link":"http://www.saltandseaweed.com/collections/vendors?q=Brandy+Melville","imageURL":"http://media-cache-ak1.pinimg.com/736x/06/49/09/06490959f16ea755c89e3b444a9e5686.jpg"}]}];


iframeWrapper = null
stepOneDoc = null
stepOneFrame = null
stepTwoFrame = null
processingAllBoards = new $.Deferred()
processingAllPins = new $.Deferred()
boardsData = []
ppPins = null
ppBoards = null
boardsPending = $(boardSelector).length
pinsPending = 'unknown'

# One-time window setup
window.ppImporter = {state: {}}
window.ppImporter.bookmarkletClosing = () ->
  $(window).off 'message', handleAnyPostedMessage

handleAnyPostedMessage = (event) ->
  if event.originalEvent.origin.replace(/^https?:/, '') != host
    console.log('Ignoring message from mismatchted source: ' + event.originalEvent.origin)
  else
    handlePostedMessage(event.originalEvent)

$(window).on 'message', handleAnyPostedMessage


reportProgress = (custom) ->
  msg = if boardsPending == 0
    "Completed all boards! Collecting pin details... ("+pinsPending+" remaining)"
  else if boardsPending == 1
    "Collecting account information: one board remaining..."
  else
    "Collecting account information: " + boardsPending + ' boards remaining...'
  msg += " (tag: "+custom+")" if custom && debug
  progressDiv.text(msg)

reportProgress('initial load')

processingAllBoards.done () ->
  reportProgress('Done processing all boards')
  window.data = boardsData
  
  # Calculate how many to do
  pinsPending = 0
  for board in boardsData
    pinsPending += board.pins.length
  reportProgress('beginning to process pins')
  
  # Now start doing them
  for board in boardsData
    processThisPin(pin) for pin in board.pins

  
ensureFrameWrapper = () ->
  iframeWrapper ?= $('<div id="ppParentPinsImporterWrapper"></div>').appendTo( outputDiv )  
  
finalizedAPin = (pin) ->
  pinsPending -= 1
  reportProgress('processed a pin: '+pin.pinterestURL)
  console.log(pin)
  if pinsPending == 0
    processingAllPins.resolve()


# Workaround for non-supporting browsers
getIframeWindow = (iframeElement) ->
  iframeElement.contentWindow || iframeElement.contentDocument.parentWindow

processThisPin = (pin) ->
  unless pin.pinterestURL
    finalizedAPin(pin)
    return
        
  pinFrame = $('<iframe>').hide().attr('src', pin.pinterestURL).appendTo( $('body') )
  console.log('opening '+pin.pinterestURL)
  pinFrame.on 'load', () ->
    pin.link = getIframeWindow(this).jQuery('.detailed.Pin.Module .pinWrapper a').first().prop('href')
    pin.imageURL = getIframeWindow(this).jQuery('.detailed.Pin.Module .pinWrapper img.pinImage').first().prop('src')
    finalizedAPin(pin)
  
# http://werxltd.com/wp/2010/05/13/javascript-implementation-of-javas-string-hashcode-method/ , converted to coffeescript, returning absolute value. Generating IDs for pinterest boards.
hashFromString = (str) ->
  hash = 0
  return hash  if @length is 0
  i = 0
  while i < @length
    char = @charCodeAt(i)
    hash = ((hash << 5) - hash) + char
    hash = hash & hash # Convert to 32bit integer
    i++
  Math.abs(hash)


if useTestData
  boardsData = testData
  processingAllPins.resolve()
else
  $(boardSelector).each (bidx, board) ->
    processingThisBoard = new $.Deferred()
    $board = $(board)
    boardURL = $board.prop('href')
    boardName = $.trim( $board.find('.boardName').text() )
    boardPinData = []

    iframeAjaxSeen_Categories = false
    iframeAjaxSeen_Board = false
    alreadyProcessingBoard = false

    iframe = $('<iframe>').hide().attr('src', boardURL).appendTo( $('body') )
    iframe.show().css({position: 'absolute', left: '50px', top: (bidx*500)+'px', height: '500px', width: '500px', border: '2px solid #333', 'z-index': 9999999999999}) if debug

    processingThisBoard.done () ->
      boardsPending -= 1
      boardsData.push({
        name: boardName,
        pinterestURL: boardURL,
        pins: boardPinData,
        id: hashFromString(boardURL)
      })
      reportProgress('finished processing board: '+boardName)
      processingAllBoards.resolve() if boardsPending == 0
    
    processSingleBoard = () ->
      $ = getIframeWindow(iframe.get(0))
      # Scroll iframe down to try lazy loading later images
      $(iframe).contents().scrollTop( $(iframe).contents().height() )
    
      iframe.contents().find(pinSelector).each (pidx, pin) ->
        $pin = $(pin)
        boardPinData.push({
          description: $pin.find('.pinDescription').text(),
          domain: $pin.find('.pinDomain').text(),
          price: $pin.find('.priceValue').text(),
          smallImageURL: $pin.find('.pinImg.loaded').prop('src'),
          pinterestURL: $pin.find('a.pinImageWrapper').prop('href')
          id: hashFromString($pin.find('a.pinImageWrapper').prop('href'))
        })
      processingThisBoard.resolve()
    
    iframe.on 'load', () ->
      # Don't fully understand, but ajaxComplete seems to only work if use the jquery from the inner window 
      # (see untested answer on http://stackoverflow.com/questions/14563041/detect-content-of-iframe-change-the-content-is-dynamic-jquery-mobile) 
      getIframeWindow(this).jQuery(getIframeWindow(this).document).ajaxComplete (event, xhr, settings) ->
        return if alreadyProcessingBoard
        iframeAjaxSeen_Categories = true if /resource\/CategoriesResource\/get/.test(settings.url)
        iframeAjaxSeen_Board      = true if /resource\/BoardFeedResource\/get/.test(settings.url)
      
        # TODO: how handle if too many pins in board, pagination required?
        if (iframeAjaxSeen_Categories && iframeAjaxSeen_Board)
          alreadyProcessingBoard = true
          setTimeout(processSingleBoard, 100)




# =====================================================
# = Add events to enable interactivity on step_1 page =
# =====================================================  

hideShowPinsForSelectedBoard = () ->
  class_to_show = stepOneDoc.find('.importing_boards li.selected').attr('class').replace(/\s*selected\s*/, '').replace(/\s*ui-\w+\s*/g, '')
  stepOneDoc.find('.importing_pins li.pin').hide()
  stepOneDoc.find('.importing_pins li.pin.' + class_to_show).show()
  checkIfAnyDraggableLeft()

checkIfAnyDraggableLeft = () ->
  stepOneDoc.find('.importing_pins').each () ->
    section = $(this)
    if section.find('li.pin:visible').length == 0
      kind = if section.hasClass('not_yet_imported') 
       'not-yet-imported'
      else
       'previously imported'
      board_label = if stepOneDoc.find('.importing_boards li.selected').first().hasClass('board-all')
        ''
      else
        'on this board'
      
      section.find('.no-more').remove()
      section.find('ul').append("<div class='no-more'>List of "+kind+" pins "+board_label+" is empty.</div>")
    else
      section.find('.no-more').remove()
  

initOnceBoardsAreLoaded = () ->
  # Add draggable/droppable effects
  dropOpts = {
    hoverClass: "ui-state-active",
    drop: (event, ui) ->
      li = $(ui.draggable)
      target = $(this).find('.ourBoardPins')
      
      if li.hasClass('pin')      
        target.append( li.css({left: 0, top: 0}) )
        setTimeout(hideShowPinsForSelectedBoard, 1)
      else # Dropped a pinterest board
        stepOneDoc.find('.importing_pins li.pin.' + li.attr('class').replace(/\s*selected\s*/, '').replace(/\s*ui-\w+\s*/g, '')).each () ->
          $(this).appendTo(target)
        hideShowPinsForSelectedBoard()
  }
  dropToPinterestOpts = {
    hoverClass: "ui-state-active",
    accept: 'li.pin',
    drop: (event, ui) ->
      li = $(ui.draggable)
      base = if li.hasClass('already-imported')
        $(this).find('.importing_pins.previously_imported ul')
      else
        $(this).find('.importing_pins.not_yet_imported ul')
    
      base.append( li.css({left: 0, top: 0}) )
      setTimeout(hideShowPinsForSelectedBoard, 1)
  }
  dragOpts = {
    revert: 'invalid',
    stack: stepOneDoc.find('#our_section li.board'),
    helper: 'clone',
    start: (event, ui) ->
      $(event.target).css({opacity: 0.5})
    stop: (event, ui) ->
      $(event.target).css({opacity: 1.0})
  }
  pinterestBoardDragOpts = {
    revert: 'invalid',
    stack: stepOneDoc.find('.importing_boards li'),
    helper: 'clone',
    start: (event, ui) ->
      $(event.target).css({opacity: 0.5})
    stop: (event, ui) ->
      $(event.target).css({opacity: 1.0})
  }
  
  # http://bugs.jqueryui.com/ticket/5727 -- draggable in child window doesn't work when initialized from parent document
  getIframeWindow(stepOneFrame[0]).initializeDraggableFromParent([
    ['.importing_pins li.pin',    'draggable', dragOpts],
    ['.importing_boards li',      'draggable', pinterestBoardDragOpts],
    ['#our_section li.board',     'droppable', dropOpts],
    ['#pinterest_section',        'droppable', dropToPinterestOpts]
  ])
    
# Odd timing issue with loading of bookmarklet SCSS, need to repaint window after step1 loaded
toggleWindowRepaint = () ->
  body = $('body').first()
  body.height( body.height() )
  
transitionToStepOne = () ->
  ensureFrameWrapper()
  stepTwoFrame.hide() if (stepTwoFrame)
  stepOneFrame ?= $('<iframe class="ppParentPinsImporter">').appendTo( iframeWrapper )
  stepOneFrame.height( outputDivWrapper.height() - outputDiv.offset().top ).show()

  stepOneDoc = stepOneFrame.contents()
  
  doc = stepOneDoc[0]
  if doc
    doc.open()
    doc.write( JST['templates/bookmarklet/pinterest/step_1']({boards: boardsData, cssUrl: stepOneCSSUrl, jsUrl: stepOneJSUrl}) )
  else
    alert("Unable to access document for stepOneFrame - maybe cross domain security issue?")
  toggleWindowRepaint()


stepOneWithImportedData = (data, resetAllAlreadyMovedPins) ->
  if ppPins # If already have some, append to the imported list
    _.each _.keys(data), (url) ->
      ppPins[url] ?= []
      ppPins[url] = _.union(ppPins[url], data[url])
  else
    ppPins = data
    
  window.ppPins = ppPins
  not_yet_imported = stepOneDoc.find('.importing_pins.not_yet_imported ul')
  imported = stepOneDoc.find('.importing_pins.previously_imported ul')

  # In case any already assigned a board, move back to main section
  if resetAllAlreadyMovedPins
    stepOneDoc.find('#our_section ul.ourBoardPins li').each () ->
      $(this).appendTo(not_yet_imported)

  # Starts with all pins in the not_yet_imported, then this moves the imported ones elsewhere
  moveToSectionIfPreviouslyImported = (li) ->
    li = $(li)
    prev_imported = _.any ppPins[li.data('pin-url')], (importedImageURL) ->
      importedImageURL == li.data('pin-image')

    if prev_imported
        li.addClass('already-imported').appendTo(imported)

  not_yet_imported.find('li.pin').each () ->
    moveToSectionIfPreviouslyImported(this)

  stepOneDoc.find('#our_section ul.ourBoardPins li').each () ->
    moveToSectionIfPreviouslyImported(this)
  

  checkIfAnyDraggableLeft()

initStepOneFunctionality = () ->
  withPPBoards = (data) ->
    ppBoards = data
    window.ppBoards = data
    
    context = {
      boards: data,
      absoluteURL: (rel) ->
        return rel if rel.match(/:\/\//)
        host + rel
      pluralize: (i, singular, plural) ->
        i + ' ' + (if i == 1 then singular else plural)
    }
    stepOneDoc.find('#our_section').html( JST['templates/bookmarklet/pinterest/our_boards'](context) )
    initOnceBoardsAreLoaded()
    

      
  
  if ppBoards
    withPPBoards(ppBoards)
  else
    $.getJSON boardsUrl, (data) ->
      withPPBoards(data)
      
  if ppPins
    stepOneWithImportedData(ppPins)
  else
    $.getJSON pinsUrl, (data) ->
      stepOneWithImportedData(data)

  stepOneDoc.find('#ppSubmitBoardsSortedLink').on 'click', () ->
    if _.isEmpty(dataToImportFromStepOne())
      alert("Drag the Pinterest pins you'd like to import down to your ParentPins boards before clicking import.")
    else
      transitionToStepTwo()

  stepOneDoc.find('#ppResetDragDropLink').on 'click', () ->
    stepOneWithImportedData(ppPins, true)
  
  stepOneDoc.find('#ppTogglePreviouslyImportedPins').on 'click', () ->
    imported = stepOneDoc.find('.importing_pins.previously_imported')
    link = $(this)
    if imported.is(':visible')
      imported.slideUp()
      link.text('Show Previously Imported')
    else
      imported.slideDown()
      link.text('Hide Previously Imported')
    checkIfAnyDraggableLeft()
  
  # Only show pins from selected board
  stepOneDoc.find('body').on 'click', '.importing_boards li', (e) ->
    li = if this.tagName == 'LI' then $(this) else $(this).parents('li').first()
    li.siblings().removeClass('selected')
    li.addClass('selected')
    hideShowPinsForSelectedBoard()  


window.ppImporter.stepOneIframeLoaded = () ->
  initStepOneFunctionality()


dataForPinID = (id) ->
  for board in boardsData
    for pin in board.pins
      return pin if pin.id == id
  null

# Render template with the pinterest data
processingAllPins.done () ->
  window.data = boardsData
  progressDiv.remove()
  transitionToStepOne()

dataToImportFromStepOne = () ->
  toImport = {}
  for board in stepOneDoc.find('#our_section li.board')
    ourBoardID = $(board).data('boardId')
    thisBoardData = []
    for pin in $(board).find('.ourBoardPins li')
      data = dataForPinID( $(pin).data('pinId') )
      thisBoardData.push(data) if data
    
    if thisBoardData.length
      toImport[ourBoardID] = thisBoardData 
  
  return toImport
  

transitionToStepTwo = () ->
  # Loop through the #our_section boards and collect pinterest data for any with pins to be imported
  toImport = dataToImportFromStepOne()
  
  # Now that we have our list of things to import, what to do?
  if _.isEmpty(toImport).length == 0
    return alert("You haven't yet selected any pins to import. Please drag at least one Pinterest pin down onto the ParentPins board you want to save it to.")

  initStepTwo(toImport)


handlePostedMessage = (event) ->
  if event.data == 'step2:previous'
    stepTwoFrame.hide()
    stepOneFrame.show()
    # Update previously imported list
    # Update UI to remove prev imported without clearing settings?
  else if event.data == 'step2:done'
    # We could hide the iframe, if we wanted...
  else if event.data.substr(0,15) == 'step2:imported:'
    json_string = event.data.substr(15, event.data.length)
    pins = $.parseJSON(json_string)
    stepOneWithImportedData(pins)
    
    
  

initStepTwo = (toImport) ->
  ensureFrameWrapper()
  stepOneFrame.hide()
  stepTwoFrame.remove() if stepTwoFrame
  stepTwoFrame = $('<iframe class="ppParentPinsImporter" name="ppParentPinsImporterStep2Frame">').appendTo( iframeWrapper )
  stepTwoFrame.height( outputDivWrapper.height() - outputDiv.offset().top ).show()
  
  # Note: loading the data as a param string inside a form so we can send a POST request (no URL length limit), but not via ajax (had authentication issues)
  stepTwoFrame.contents()[0].write( JST['templates/bookmarklet/pinterest/step_2_load']({data_string: $.param(toImport), stepTwoURL: stepTwoURL}) )