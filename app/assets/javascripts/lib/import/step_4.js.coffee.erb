sendMessage = (msg, explanation) ->
  if (!window.opener)
    console.log("Sorry, browser doesn't appear to support connecting to the parent window.")
  else if (!window.opener.postMessage)
    console.log("Sorry, your browser appears too old to support modern web standards.")
  else
    window.opener.postMessage(msg, '*')

window.importedPins = (pins_as_string) ->
  sendMessage("step4:imported:#{pins_as_string}")

window.importCompleted = () ->
  sendMessage("step4:done")

updateOtherStatus = (fields) ->
  updateStatusField fields, '.otherStatus'

updateAgeGroupStatus = (fields) ->
  updateStatusField fields, '.ageGroupStatus'

updatePinTypeStatus = (fields) ->
  updateStatusField fields, '.pinTypeStatus'
    
updateStatusField = (fields, statusSelector) ->
  li = $(fields).first().parents('li').first()
  target = li.find(statusSelector)
  anyBlank = false
    
  $(fields).each () ->
    if $(this).is(':radio')
      name = $(this).attr('name')
      value = $(this).parent().find('input:radio[name="'+name+'"]:checked').val()
      anyBlank = true if !value
    else
      if $(this).val() == ''
        anyBlank = true
  
  if anyBlank
    target.removeClass('complete').addClass('pending')
  else
    target.removeClass('pending').addClass('complete')
  
  if li.find('.status_boxes .status_box').length == li.find('.status_boxes .status_box.complete').length
    li.addClass('complete')
  else
    li.removeClass('complete')
  

window.toggleImportingThisPin = (link) ->
  li = $(link).parents('li.importing_pin')
  
  if (li.hasClass('skip-this-pin'))
    li.removeClass('skip-this-pin')

    $('#'+li.attr('id')+'_pin_info').insertAfter( li.find('.pin_photo') ).show()
    li.find('.pin_errors').show()
    li.find('.status_boxes').show()
    li.find('.open_close_controls a').text('Remove pin from import')
  else
    li.addClass('skip-this-pin')
    
    # Hide various UI components
    li.find('.pin_info').hide()
    li.find('.pin_errors').hide()
    li.find('.status_boxes').hide()
    li.find('.open_close_controls a').text('Import this pin')

    # Now actually move .pin_info outside the form, so it doesn't submit
    li.find('.pin_info').insertAfter( li.parents('form') )

updatePinSelectionFromBoardCheckbox = (checkbox) ->
  $(checkbox).parents('li.importing_board').find('input[name=mass-select]').each (idx, pinbox) ->
    $(pinbox).prop('checked', $(checkbox).is(':checked'))
    updatePinSelectionFromCheckbox(pinbox)

updatePinSelectionFromCheckbox = (checkbox) ->
  checkbox = $(checkbox)
  pin = checkbox.parents('li.importing_pin')
  if checkbox.is(':checked') then pin.addClass('mass-selected') else pin.removeClass('mass-selected')
  updateMassEditingControlsVisibility()

updateMassEditingControlsVisibility = () ->
  mass = $('#mass-edit-controls')
  if count = $('input[name=mass-select]:checked').length
    mass.find('.count .number').text(count)
    unless mass.is(':visible')
      mass.find('select').val('')
      mass.find('input:checked').prop('checked', null)
      mass.fadeIn()
  else
    mass.fadeOut()

handleHideShowPriceField = (input) ->
  input = $(input)
  extrafield = input.parents('li').first().find('.field-price')
  if input.val() == 'product'
    extrafield.slideDown()
  else
    if input.data('animate') then extrafield.slideUp() else extrafield.hide()
  input.data('animate', true)
  

$(document).ready () ->
  return unless $('.context.step_4').length
  
  # Preload loading image
  img = new Image()
  img.src = "<%= asset_path('ui/ball-loader.gif') %>"
  
  # For wide monitors, allow viewing side by side without weird float clearing for long images
  applyMasonry('ul.importing_pins')
  
  form = $('#import_form')
    
  form.on 'submit', (e) ->
    if form.find('li.importing_pin.complete').length == 0 && !form.data('go-back')
      alert('You need to fill in all required fields for each pin before we can complete the import.')
      return false

    # Once ready to submit, only process one pin at a time (to avoid server timing out + give user better UX)
    # TOOD: update UI to show uploading
    data = form.serializeObject()
    disableForm(form)
    e.preventDefault()
    
    # Collate data broken down by pin (along with HTMl references so we can update the UI)
    sortedData = []
    for boardLI in form.find('li.importing_board')
      for pinLI in $(boardLI).find('li.importing_pin')
        pinID = "pins[[#{$(boardLI).data('board-id')}.#{$(pinLI).data('pin-id')}]]"
        pinData = {board: boardLI, pin: pinLI, data: {}}
        pinData['data'][key] = value for key, value of data when key.indexOf(pinID) > -1
        sortedData.push(pinData)
    
    totalPinsToUpload = sortedData.length
    
    handleBeforeAnySubmitted = () ->
      $('.progress_wrapper').append """
        <div id="progressDisplayWrapper">
          <div id="progressDisplay">Processing pins...</div>
        </div>
      """
      updateProgressBar()
    
    updateProgressBar = () ->
      maxProgressBarWidth = 424
      remaining = sortedData.length
      completed = totalPinsToUpload - remaining
      width = if totalPinsToUpload > 0 then maxProgressBarWidth * (completed / totalPinsToUpload) else maxProgressBarWidth
      bar = "<div class='progress_bar'><div class='progress' style='width:#{width}px'></div></div>"
      msg = if completed == totalPinsToUpload
          "Finished uploading pins!"
        else
          "Uploading #{completed + 1} of #{totalPinsToUpload}"
      $('#progressDisplayWrapper').html "#{bar} #{msg}"
    
    
    handleBeforeOneSubmitted = (which) ->
      info = $(which.pin).addClass('ui-importing').children('.pin_info')
      info.data('original-content', info.html())
      info.html("<img src='<%= asset_path('ui/ball-loader.gif') %>' alt='submitting...'/>").css({textAlign: 'center'})
    
    # How handle success/failure
    handleSuccess = (which, resp) ->
      li = $(which.pin).removeClass('ui-importing').addClass('ui-imported')
      li.children(':not(.pin_photo)').remove()
      li.children('.pin_photo').wrapInner("<a href='#{resp}' target='_blank'></a>")
      li.animate {width: 80, height: 90}, () ->
        $('ul.importing_pins').masonry('layout')

    handleFailure = (which, resp) ->
      li = $(which.pin).removeClass('ui-importing').addClass('ui-import-failed')
      info = li.find('.pin_info')
      info.html( info.data('original-content') )
      errors = if li.find('.pin_errors').length then li.find('.pin_errors') else $('<div class="pin_errors"></div>').prependTo(li)
      errors.html(resp.responseText)
      $('ul.importing_pins').masonry('layout')
    
    handleAnyResponse = (which) ->
      # Handle progress bar UI
      updateProgressBar()
      
      # And try another one
      processOnePin()
      
    
    # Now go to work
    processOnePin = () ->
      if pin = sortedData.shift()
        handleBeforeOneSubmitted(pin)
        ajax = $.post "#{form.attr('action')}_incremental", pin.data
        ajax.always ()   -> handleAnyResponse(pin)
        ajax.done (resp) -> handleSuccess(pin, resp)
        ajax.fail (resp) -> handleFailure(pin, resp)
          
    handleBeforeAnySubmitted()
    processOnePin()
        

    

  
  form.find('li.importing_pin').each () ->
    updateOtherStatus( $(this).find('.other_input') )
    
    inputToTest = if $(this).find('input.pin_type:checked').length
      $(this).find('input.pin_type:checked')
    else
      $(this).find('input.pin_type').first()
    updatePinTypeStatus( inputToTest )
    handleHideShowPriceField(inputToTest)

  form.find('select.age_group_id').each () ->
    updateAgeGroupStatus(this)
    
  # Handle marking pin type selected, toggle price fields
  form.on 'change', 'input.pin_type', () ->
    handleHideShowPriceField(this)
    updatePinTypeStatus(this)
      
  # Handle marking age group selected
  form.on 'change', 'select.age_group_id', () ->
    updateAgeGroupStatus(this)

  # Handle updating on other inputs
  form.on 'change keyup keypress', '.other_input', () ->
    updateOtherStatus( $(this).parents('li.importing_pin').find('.other_input') )

  # Set up pin/board selection checkboxes (initial + when changed)
  
  form.find('input[name=mass-select]').each () ->
    updatePinSelectionFromCheckbox(this)
    
  form.on 'change', 'input[name=mass-select]', () ->
    updatePinSelectionFromCheckbox(this)

  form.find('input[name=mass-select-board]').each () ->
    updatePinSelectionFromBoardCheckbox(this)
    
  form.on 'change', 'input[name=mass-select-board]', () ->
    updatePinSelectionFromBoardCheckbox(this)

  mass = $('#mass-edit-controls')
  mass.on 'change', 'select[name=age_group_id]', () ->
    newVal = $(this).val()
    form.find('.mass-selected select.age_group_id').val( newVal ).each () ->
      updateAgeGroupStatus(this)
  
  mass.on 'change', 'input', () ->
    if $(this).is(':checked')
      form.find(".mass-selected input.pin_type[value='#{$(this).val()}']").prop('checked', 'checked').each () ->
        updatePinTypeStatus(this)